plugins {
	id 'multiloader-loader'
	id 'fabric-loom'
	id 'com.modrinth.minotaur' version '2.+'
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
	}

	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
	implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
	implementation project(":common")
	implementation "com.google.auto.service:auto-service:1.1.1"
	annotationProcessor "com.google.auto.service:auto-service:1.1.1"

	modCompileOnly "org.valkyrienskies:valkyrienskies-120-fabric:${vs2_version}"
	modRuntimeOnly "org.valkyrienskies.core:api:${vs_core_version}"
	modRuntimeOnly "org.valkyrienskies:valkyrienskies-120-fabric:${vs2_version}"

	modCompileOnly "maven.modrinth:jade:${jade_version}+fabric"
	modRuntimeOnly "maven.modrinth:jade:${jade_version}+fabric"
	modCompileOnly "com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}"
	modRuntimeOnly "com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}"
}

loom {
	if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
		accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
	}
	mixin {
		defaultRefmapName.set("${mod_id}.refmap.json")
	}
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "vsprinter"
	versionNumber = "${minecraft_version}-fabric-${version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions = ["1.20.1"]
	loaders = ["fabric"]
	dependencies {
		// required.project "jade"
		// required.project "valkyrien-skies"
	}
}
